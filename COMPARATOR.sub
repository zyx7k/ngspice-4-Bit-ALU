.include NOT.sub
.include XNOR.sub
.include AND.sub
.include 3_AND.sub
.include 4_AND.sub
.include 5_AND.sub
.include 4_OR.sub

.subckt COMPARATOR A0 A1 A2 A3 B0 B1 B2 B3 A_gt_B A_eq_B A_st_B vdd gnd

*A_gt_B : means A greater than B
*A_eq_B means A equal to B
*A_st_B means A smaller than B

X1 A0 B0 node0 vdd gnd XNOR
X2 A1 B1 node1 vdd gnd XNOR
X3 A2 B2 node2 vdd gnd XNOR
X4 A3 B3 node3 vdd gnd XNOR
X5 node0 node1 node2 node3 A_eq_B vdd gnd 4_AND

X6 A0 A0_inv vdd gnd NOT
X7 A1 A1_inv vdd gnd NOT
X8 A2 A2_inv vdd gnd NOT
X9 A3 A3_inv vdd gnd NOT
X10 B0 B0_inv vdd gnd NOT
X11 B1 B1_inv vdd gnd NOT
X12 B2 B2_inv vdd gnd NOT
X13 B3 B3_inv vdd gnd NOT

X14 A3 B3_inv node4 vdd gnd AND
X15 A2 B2_inv node3 node5 vdd gnd 3_AND
X16 A1 B1_inv node3 node2 node6 vdd gnd 4_AND
X17 A0 B0_inv node3 node2 node1 node7 vdd gnd 5_AND 
X18 node4 node5 node6 node7 A_gt_B vdd gnd 4_OR

X19 B3 A3_inv node8 vdd gnd AND
X20 B2 A2_inv node3 node9 vdd gnd 3_AND
X21 B1 A1_inv node3 node2 node10 vdd gnd 4_AND
X22 B0 A0_inv node3 node2 node1 node11 vdd gnd 5_AND 
X23 node8 node9 node10 node11 A_st_B vdd gnd 4_OR

.ends COMPARATOR